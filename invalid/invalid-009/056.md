Sneaky Chrome Sealion

medium

# Missing checks for return value of approve

## Summary
When using `approve`, some ERC20 implementations return a boolean on failure rather than reverting. The `JalaMasterRouter.sol` contract, despite mentioning that there's no need to check the `approve` return value, may execute functions without approving anything.

## Vulnerability Detail
Throughout the `JalaMasterRouter.sol` contract, `approve` is used for adding, removing, and swapping liquidity. There are comments in the contract that mention there is `"no need for check return value, bc addliquidity will revert if approve was declined."`. 

However, the `addliquidity` function is not called when removing or swapping liquidity. Therefore, these functionalities may execute despite the tokens not approving.

## Impact
Operations will potentially execute despite approval failing, leading to complications and potential loss of funds.

## Code Snippet
For reference, here is the mention that `addliquidity` will revert if approve was declined. But it's only true in the functions that actually call `addliquidity`.

`JalaMasterRouter::wrapTokensAndaddLiquidity` [#L32-69](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L32-L69)
```javascript
    function wrapTokensAndaddLiquidity(
        // only use wrapTokensAndaddLiquidity to create pool.
        address tokenA, // origin token
        address tokenB, // origin token
        uint256 amountADesired, // unwrapped.
        uint256 amountBDesired, // unwrapped.
        uint256 amountAMin, // unwrapped
        uint256 amountBMin, // unwrapped
        address to,
        uint256 deadline
    ) public virtual override returns (uint256 amountA, uint256 amountB, uint256 liquidity) {
        // get token from user
        TransferHelper.safeTransferFrom(tokenA, msg.sender, address(this), amountADesired);
        TransferHelper.safeTransferFrom(tokenB, msg.sender, address(this), amountBDesired);

        address wrappedTokenA = _approveAndWrap(tokenA, amountADesired);
        address wrappedTokenB = _approveAndWrap(tokenB, amountBDesired);

        uint256 tokenAOffset = IChilizWrappedERC20(wrappedTokenA).getDecimalsOffset();
        uint256 tokenBOffset = IChilizWrappedERC20(wrappedTokenB).getDecimalsOffset();

@>      IERC20(wrappedTokenA).approve(router, IERC20(wrappedTokenA).balanceOf(address(this))); // no need for check return value, bc addliquidity will revert if approve was declined.
        IERC20(wrappedTokenB).approve(router, IERC20(wrappedTokenB).balanceOf(address(this)));

        // add liquidity
        (amountA, amountB, liquidity) = IJalaRouter02(router).addLiquidity(
            wrappedTokenA,
            wrappedTokenB,
            amountADesired * tokenAOffset,
            amountBDesired * tokenBOffset,
            amountAMin * tokenAOffset,
            amountBMin * tokenBOffset,
            to,
            deadline
        );
        _unwrapAndTransfer(wrappedTokenA, to);
        _unwrapAndTransfer(wrappedTokenB, to);
    }
```

Here are the instances where approve may fail and the return is not checked. It's important to note that the in-line comments still mention that `addliquidity` will revert, but `addliquidity` is not called throughout these functions:

`JalaMasterRouter::removeLiquidityAndUnwrapToken` [#L101-149](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L101-L149)
```javascript
    function removeLiquidityAndUnwrapToken(
        address tokenA, // token origin addr
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    ) external virtual override returns (uint256 amountA, uint256 amountB) {
        address wrappedTokenA = IChilizWrapperFactory(wrapperFactory).wrappedTokenFor(tokenA);
        address wrappedTokenB = IChilizWrapperFactory(wrapperFactory).wrappedTokenFor(tokenB);
        address pair = JalaLibrary.pairFor(factory, wrappedTokenA, wrappedTokenB);
        TransferHelper.safeTransferFrom(pair, msg.sender, address(this), liquidity);

        IERC20(pair).approve(router, liquidity);

        (amountA, amountB) = IJalaRouter02(router).removeLiquidity(
            wrappedTokenA,
            wrappedTokenB,
            liquidity,
            amountAMin,
            amountBMin,
            address(this),
            deadline
        );

        uint256 tokenAOffset = IChilizWrappedERC20(wrappedTokenA).getDecimalsOffset();
        uint256 tokenBOffset = IChilizWrappedERC20(wrappedTokenB).getDecimalsOffset();
        uint256 tokenAReturnAmount = (amountA / tokenAOffset) * tokenAOffset;
        uint256 tokenBReturnAmount = (amountB / tokenBOffset) * tokenBOffset;

@>      IERC20(wrappedTokenA).approve(wrapperFactory, tokenAReturnAmount); // no need for check return value, bc addliquidity will revert if approve was declined.
@>      IERC20(wrappedTokenB).approve(wrapperFactory, tokenBReturnAmount); // no need for check return value, bc addliquidity will revert if approve was declined.

        if (tokenAReturnAmount > 0) {
            IChilizWrapperFactory(wrapperFactory).unwrap(to, wrappedTokenA, tokenAReturnAmount);
        }
        if (tokenBReturnAmount > 0) {
            IChilizWrapperFactory(wrapperFactory).unwrap(to, wrappedTokenB, tokenBReturnAmount);
        }

        // transfer dust as wrapped token
        if (amountA - tokenAReturnAmount > 0) {
            TransferHelper.safeTransfer(wrappedTokenA, to, amountA - tokenAReturnAmount);
        }
        if (amountB - tokenBReturnAmount > 0) {
            TransferHelper.safeTransfer(wrappedTokenB, to, amountB - tokenBReturnAmount);
        }
    }
```

`JalaMasterRouter::removeLiquidityETHAndUnwrap` [#L151-189](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L151-L189)
```javascript
    function removeLiquidityETHAndUnwrap(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    ) public virtual override returns (uint256 amountToken, uint256 amountETH) {
        address wrappedToken = IChilizWrapperFactory(wrapperFactory).wrappedTokenFor(token);
        address pair = JalaLibrary.pairFor(factory, wrappedToken, address(WETH));
        TransferHelper.safeTransferFrom(pair, msg.sender, address(this), liquidity);

    @>  IERC20(pair).approve(router, liquidity);

        (amountToken, amountETH) = IJalaRouter02(router).removeLiquidityETH(
            wrappedToken,
            liquidity,
            amountTokenMin,
            amountETHMin,
            address(this),
            deadline
        );

        uint256 tokenOffset = IChilizWrappedERC20(wrappedToken).getDecimalsOffset();
        uint256 tokenReturnAmount = (amountToken / tokenOffset) * tokenOffset;

 @>     IERC20(wrappedToken).approve(wrapperFactory, tokenReturnAmount); // no need for check return value, bc addliquidity will revert if approve was declined.

        if (tokenReturnAmount > 0) {
            IChilizWrapperFactory(wrapperFactory).unwrap(to, wrappedToken, tokenReturnAmount);
        }
        // transfer dust as wrapped token
        if (amountToken - tokenReturnAmount > 0) {
            TransferHelper.safeTransfer(wrappedToken, to, amountToken - tokenReturnAmount);
        }

        (bool success, ) = to.call{value: address(this).balance}("");
        require(success);
    }
```

`JalaMasterRouter::swapExactTokensForTokens` [#L191-215](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L151-L189](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L191-L215))
```javascript
    function swapExactTokensForTokens(
        address originTokenAddress,
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external virtual override returns (uint256[] memory amounts, address reminderTokenAddress, uint256 reminder) {
        address wrappedTokenIn = IChilizWrapperFactory(wrapperFactory).wrappedTokenFor(originTokenAddress);

        require(path[0] == wrappedTokenIn, "MS: !path");

        TransferHelper.safeTransferFrom(originTokenAddress, msg.sender, address(this), amountIn);
        _approveAndWrap(originTokenAddress, amountIn);
@>      IERC20(wrappedTokenIn).approve(router, IERC20(wrappedTokenIn).balanceOf(address(this)));

        amounts = IJalaRouter02(router).swapExactTokensForTokens(
            IERC20(wrappedTokenIn).balanceOf(address(this)),
            amountOutMin,
            path,
            address(this),
            deadline
        );
        (reminderTokenAddress, reminder) = _unwrapAndTransfer(path[path.length - 1], to);
    }
```

`JalaMasterRouter::swapExactTokensForETH` [#L284-301](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L284-L301)
```javascript
    function swapExactTokensForETH(
        address originTokenAddress,
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external virtual override returns (uint256[] memory amounts) {
        address wrappedTokenIn = IChilizWrapperFactory(wrapperFactory).wrappedTokenFor(originTokenAddress);

        require(path[0] == wrappedTokenIn, "MS: !path");

        TransferHelper.safeTransferFrom(originTokenAddress, msg.sender, address(this), amountIn);
        _approveAndWrap(originTokenAddress, amountIn);
@>      IERC20(wrappedTokenIn).approve(router, IERC20(wrappedTokenIn).balanceOf(address(this)));

        amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn, amountOutMin, path, to, deadline);
    }
```

`JalaMasterRouter::_unwrapAndTransfer` [#L303-326](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L303-L326)
```javascript
    function _unwrapAndTransfer(
        address wrappedTokenOut,
        address to
    ) private returns (address reminderTokenAddress, uint256 reminder) {
        // address wrappedTokenOut = path[path.length - 1];
        uint256 balanceOut = IERC20(wrappedTokenOut).balanceOf(address(this));
        if (balanceOut == 0) return (reminderTokenAddress, reminder);

        uint256 tokenOutOffset = IChilizWrappedERC20(wrappedTokenOut).getDecimalsOffset();
        uint256 tokenOutReturnAmount = (balanceOut / tokenOutOffset) * tokenOutOffset;

@>      IERC20(wrappedTokenOut).approve(wrapperFactory, tokenOutReturnAmount); // no need for check return value, bc addliquidity will revert if approve was declined.

        if (tokenOutReturnAmount > 0) {
            IChilizWrapperFactory(wrapperFactory).unwrap(to, wrappedTokenOut, tokenOutReturnAmount);
        }

        // transfer dust as wrapped token
        if (IERC20(wrappedTokenOut).balanceOf(address(this)) > 0) {
            reminderTokenAddress = address(wrappedTokenOut);
            reminder = IERC20(wrappedTokenOut).balanceOf(address(this));
            TransferHelper.safeTransfer(wrappedTokenOut, to, IERC20(wrappedTokenOut).balanceOf(address(this)));
        }
    }
```

`JalaMasterRouter::_approveAndWrap` [#L328-331](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L328-L331)
```javascript
    function _approveAndWrap(address token, uint256 amount) private returns (address wrappedToken) {
@>      IERC20(token).approve(wrapperFactory, amount); // no need for check return value, bc addliquidity will revert if approve was declined.
        wrappedToken = IChilizWrapperFactory(wrapperFactory).wrap(address(this), token, amount);
    }    
```

## Tool used

Manual Review

## Recommendation
Check the boolean return on `approve` for removing and swapping liquidity.