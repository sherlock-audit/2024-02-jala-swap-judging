Stable Hazelnut Jellyfish

medium

# `JalaPair::_mintFee` if `feeToSetter` decides to stop accrueing fees, sets `feeTo` to address(0), then we turn the fees back on, they will not accrue on the first liquidity event

## Summary
The problem is that the `kLast` variable inside `JalaPair` token always zero when we fees are off.

## Vulnerability Detail
First let's understand why [`JalaPair::_mintFee`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaPair.sol#L110C1-L129C6) gets called. It sends fees in pair tokens to the `JalaFactory::feeTo` address. This function gets called in every [mint](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaPair.sol#L139) and [burn](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaPair.sol#L193) of pair tokens, which are called on every deposit into the liquidity pool and withdrawal from it, respectively.

Now let's look into the function itself.

<details>
<summary>`JalaPair::_mintFee`</summary>

```javascript
    function _mintFee(uint112 _reserve0, uint112 _reserve1) private returns (bool feeOn) {
        address feeTo = IJalaFactory(factory).feeTo(); // get feeTo address
        feeOn = feeTo != address(0);
        uint256 _kLast = kLast; // gas savings
        if (feeOn) {
            if (_kLast != 0) {
                uint256 rootK = Math.sqrt(uint256(_reserve0) * _reserve1);
                uint256 rootKLast = Math.sqrt(_kLast);
                if (rootK > rootKLast) {
                    uint256 numerator = totalSupply * (rootK - rootKLast);
                    uint256 denominator = rootK + rootKLast;
                    uint256 liquidity = numerator / denominator;
                    // distribute LP fee
                    if (liquidity > 0) _mint(feeTo, liquidity);
                }
            }
        } else if (_kLast != 0) {
            kLast = 0;
        }
    }
```

</details>

First we get the `feeTo` address from Factory. Then if it's not 0, the `feeOn` bool is set to true. Let's assume that it's all fine and protocol accrues fees as it should to when this function gets called.

Then if the `feeTo` address is set to 0 in [`JalaFactory::setFeeTo`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaFactory.sol#L60C1-L63C6), then in `_mintFee` the `feeOn` bool is set to false. Therefore, we don't pass the `if (feeOn)` statement and go into the `else if (_kLast != 0)` block. 

Before setting the `feeTo` address to 0, the `feeOn` bool was always true and kLast was always updated in [`JalaPair::mint`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaPair.sol#L156) and in [`JalaPair::burn`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaPair.sol#L205). Therefore we pass the `else if (_kLast != 0)` and set `kLast` to 0, and it will stay zero until `feeOn` is set to true.

Then, we set the `feeTo` address, thus, `feeOn` is also true now and we pass that check inside `_mintFee` function. The problem is that before actually calculating and minting the fee, we check `if (kLast != 0)`. As you remember it's always 0 when `feeOn` is false, therefore, we don't pass that check and don't mint any fees to the protocol. Then as I've mentioned earlier the kLast is updated at the end of mint and burn functions, therefore, this issue will arise only on the first time after reseting the `feeTo` address.

## Impact
Protocol will lose funds, yes it's not much (but it's honest work), but still a loss, therefore, it follows the Sherlock's rules for medium issues (loss of funds under certain conditions).

## Code Snippet
Provided in the issue text above.

## Tool used

Manual Review

## Recommendation
Don't set the kLast value to 0, update it on every mint and burn (example is mint function, do the same for burn).
```diff
function mint(address to) external lock returns (uint256 liquidity) {
        (uint112 _reserve0, uint112 _reserve1, ) = getReserves(); // gas savings
        uint256 balance0 = IERC20(token0).balanceOf(address(this));
        uint256 balance1 = IERC20(token1).balanceOf(address(this));
        uint256 amount0 = balance0 - _reserve0;
        uint256 amount1 = balance1 - _reserve1;

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint256 _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            if (IJalaFactory(factory).migrators(msg.sender)) {
                liquidity = IMigrator(msg.sender).desiredLiquidity();
                if (liquidity == 0 || liquidity == type(uint).max) revert BadDesiredLiquidity();
            } else {
                liquidity = Math.sqrt(amount0 * amount1) - MINIMUM_LIQUIDITY;
                _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
            }
        } else {
            liquidity = Math.min((amount0 * _totalSupply) / _reserve0, (amount1 * _totalSupply) / _reserve1);
        }
        if (liquidity == 0) revert InsufficientLiquidityMinted();
        _mint(to, liquidity);

        _update(balance0, balance1, _reserve0, _reserve1);
-       if (feeOn) kLast = uint256(reserve0) * reserve1; // reserve0 and reserve1 are up-to-date
+      kLast = uint256(reserve0) * reserve1; 
        emit Mint(msg.sender, amount0, amount1);
    }
```

`_mintFee`
```diff
    function _mintFee(uint112 _reserve0, uint112 _reserve1) private returns (bool feeOn) {
        address feeTo = IJalaFactory(factory).feeTo(); // get feeTo address
        feeOn = feeTo != address(0);
        uint256 _kLast = kLast; // gas savings
        if (feeOn) {
            if (_kLast != 0) {
                uint256 rootK = Math.sqrt(uint256(_reserve0) * _reserve1);
                uint256 rootKLast = Math.sqrt(_kLast);
                if (rootK > rootKLast) {
                    uint256 numerator = totalSupply * (rootK - rootKLast);
                    uint256 denominator = rootK + rootKLast;
                    uint256 liquidity = numerator / denominator;
                    // distribute LP fee
                    if (liquidity > 0) _mint(feeTo, liquidity);
                }
            }
-       } else if (_kLast != 0) {
-           kLast = 0;
        }
    }
```