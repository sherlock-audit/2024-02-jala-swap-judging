Rough Jade Shrimp

high

# JalaPair.sol#swap() - `feeTo` won't receive any fees if users are flash loaning an unwrapped token due to rounding errors

## Summary
Users can create a pair with a wrapped and unwrapped token, which will break accounting for LP tokens

## Vulnerability Detail
The sponsors have stated in the discord channel
![image](https://github.com/sherlock-audit/2024-02-jala-swap-0xdeth/assets/65718191/6c945757-9812-4ec4-b9af-1dd44f664248)

Let's take a look why this is an issue in the context of flash loaning.

`JalaPair` has a function called `swap`, which has a flash loan mechanism, which is enabled if `flashLoan = true` and `data.length > 0`

```solidity
 function swap(uint256 amount0Out, uint256 amount1Out, address to, bytes calldata data) external lock {
        if (amount0Out == 0 && amount1Out == 0) revert InsufficientOutputAmount();
        (uint112 _reserve0, uint112 _reserve1, ) = getReserves(); // gas savings
        if (amount0Out > _reserve0 || amount1Out > _reserve1) revert InsufficientLiquidity();

        uint256 balance0;
        uint256 balance1;
        {
            // scope for _token{0,1}, avoids stack too deep errors
            address _token0 = token0;
            address _token1 = token1;
            if (to == _token0 || to == _token1) revert InvalidTo();
            if (amount0Out > 0) _safeTransfer(_token0, to, amount0Out); // optimistically transfer tokens
            if (amount1Out > 0) _safeTransfer(_token1, to, amount1Out); // optimistically transfer tokens
            if (IJalaFactory(factory).flashOn() && data.length > 0) {
                if (amount0Out > 0) {
                    _safeTransfer(
                        _token0,
                        IJalaFactory(factory).feeTo(),
                        (amount0Out * IJalaFactory(factory).flashFee()) / 10000
                    );
                    amount0Out = (amount0Out * (10000 + IJalaFactory(factory).flashFee())) / 10000;
                }
                if (amount1Out > 0) {
                    _safeTransfer(
                        _token1,
                        IJalaFactory(factory).feeTo(),
                        (amount1Out * IJalaFactory(factory).flashFee()) / 10000
                    );
                    amount1Out = (amount1Out * (10000 + IJalaFactory(factory).flashFee())) / 10000;
                }
                IJalaCallee(to).JalaCall(msg.sender, amount0Out, amount1Out, data);
            }
            balance0 = IERC20(_token0).balanceOf(address(this));
            balance1 = IERC20(_token1).balanceOf(address(this));
        }
        uint256 amount0In = balance0 > _reserve0 - amount0Out ? balance0 - (_reserve0 - amount0Out) : 0;
        uint256 amount1In = balance1 > _reserve1 - amount1Out ? balance1 - (_reserve1 - amount1Out) : 0;
        if (amount0In == 0 && amount1In == 0) revert InsufficientInputAmount();
        {
            // scope for reserve{0,1}Adjusted, avoids stack too deep errors
            uint256 balance0Adjusted = (balance0 * 1000) - (amount0In * 3);
            uint256 balance1Adjusted = (balance1 * 1000) - (amount1In * 3);
            if (balance0Adjusted * balance1Adjusted < uint256(_reserve0) * uint256(_reserve1) * (1000 ** 2))
                revert InvalidK();
        }

        _update(balance0, balance1, _reserve0, _reserve1);
        emit Swap(msg.sender, amount0In, amount1In, amount0Out, amount1Out, to);
    }
``` 

Most of the code is a fork of UniV2, with the exception of the added fee logic. Basically when a user takes out a flash loan, some percentage of the amount of tokens being used for the swap, will be paid to the `feeTo` address, which is a protocol address.

Knowing that unwrapped tokens, which have 0 decimals, can be used as `token0` or `token1`, because of rounding issues, 0 fees will be paid to `feeTo`

Example:
`amount0Out = 999`
`flashFee = 10(0.1%)`

`(amount0Out * IJalaFactory(factory).flashFee()) / 10000`
`(999 * 10) / 10000 = 0.99` which rounds down to `0`

Depending on the `flashFee` users can specify just enough tokens to make the equation round down to 0, because of this `feeTo` won't receive any of the fees.

## Impact
`feeTo` won't receive any funds

## Code Snippet
https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaPair.sol#L150

## Tool used
Manual Review

## Recommendation
Because fan tokens have 0 decimals, the fix isn't very straightforward. One way is to create a special pair that handles unwrapped fan tokens correctly.
