Daring Sky Piranha

medium

# `feeTo` address used to determine if `feeOn` should be true isn't consistent with UniswapV2

## Summary
The code forked from UniswapV2 uses an address `feeTo`, the place where fees are sent, in order to determine if the fee is currently enabled. The issue is that Uniswap, and now Jalaswap, checks to see if `feeTo` is equal to `address(0)`. If `feeTo != address(0)` is true, then `feeOn` is set to true and considered enabled. 

## Vulnerability Detail
The problem is that UniswapV2 doesn't set `feeTo` to a variable on deployment, and `JalaFactory` sets `feeTo` to `0x000000000000000000000000000000000000dEaD` inside the constructor. This means the code determining if the fee is currently enabled will return true even when the `feeTo` address is `0x000000000000000000000000000000000000dEaD`. 

## Impact
Since the contract will mistakenly believe the fee is enabled, it will send funds to the `feeTo` address, which will then be lost forever. 

This function shows that the variables will return false when compared:

```solidity
function isEqual() public pure returns(bool) {
    return (address(0) == 0x000000000000000000000000000000000000dEaD); // false
  }
```

## Code Snippet

[Here](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaFactory.sol#L26) is the `JalaFactory.sol` code that initially sets `feeTo` in the constructor: 

```solidity
 address public constant DEAD = 0x000000000000000000000000000000000000dEaD;

    bool public override flashOn;
    uint public override flashFee;
    address public override feeTo;
    address public override feeToSetter;

    constructor(address _feeToSetter) {
        feeToSetter = _feeToSetter;
        flashOn = false;
        feeTo = DEAD;
    }
```

[Here](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaPair.sol#L112) is the `_mintFee()` function that will incorrectly assume the fee is enabled and then mint `liquidity` to `feeTo`:

```solidity
  // if fee is on, mint liquidity equivalent to 1/2th of the growth in sqrt(k)
    function _mintFee(uint112 _reserve0, uint112 _reserve1) private returns (bool feeOn) {
        address feeTo = IJalaFactory(factory).feeTo(); // get feeTo address
        feeOn = feeTo != address(0);
        uint256 _kLast = kLast; // gas savings
        if (feeOn) {
            if (_kLast != 0) {
                uint256 rootK = Math.sqrt(uint256(_reserve0) * _reserve1);
                uint256 rootKLast = Math.sqrt(_kLast);
                if (rootK > rootKLast) {
                    uint256 numerator = totalSupply * (rootK - rootKLast);
                    uint256 denominator = rootK + rootKLast;
                    uint256 liquidity = numerator / denominator;
                    // distribute LP fee
                    if (liquidity > 0) _mint(feeTo, liquidity);
                }
            }
        } else if (_kLast != 0) {
            kLast = 0;
        }
    }
```

## Tool used

Manual Review

## Recommendation
I recommend that either the `feeTo` address isn't initialized with a value at all, so that by default it will be `address(0)`:

```solidity
   address public feeTo;
    address public feeToSetter;

    mapping(address => mapping(address => address)) public getPair;
    address[] public allPairs;

    event PairCreated(address indexed token0, address indexed token1, address pair, uint);

    constructor(address _feeToSetter) public {
        feeToSetter = _feeToSetter;
    }
```


Or that you check to see if the `feeTo` address is `address(0)` or `0x000000000000000000000000000000000000dEaD`:

```solidity
 feeOn = (feeTo != address(0) && feeTo != 0x000000000000000000000000000000000000dEaD); 
```



