Odd Arctic Tadpole

high

# Tokens that transfer of less than amount can be used to still funds

## Summary

Some ERC20 tokens (e.g., cUSDCv3) contain a special case for amount == type(uint256).max in their transfer functions that results in only the user's balance being transferred. This can be used to still users funds from chillz wrapped token.

## Vulnerability Detail

Let's assume we have an underlying token that is described in [Weird ERC20](https://github.com/d-xo/weird-erc20?tab=readme-ov-file#transfer-of-less-than-amount), and has the property that transfers max balance for `type(uint256).max`.

Attack scenario:
1. The attacker creates a wrapper for the underlying token
2. The attacker takes a flash loan for 1mln amount of the underlying token. 
3. The attacker does the first deposit, by calling [depositFor](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/utils/ChilizWrappedERC20.sol#L33) to the wrapper by sending `type(uint256).max` as amount. This action transfers 1 mln amount of tokens to the wrapper contract, but the wrapper contract writes in the storage that the attacker has all token supply.
4. The attacker withdraws a 1 mln token amount, and repays the flash loan (it might be that he will have to cover flashloan fee by himself). The withdrawal step is important because it lets other users deposit. Without this step other users cannot deposit, as the total supply would revert due to overflow. At this point, the attacker has `type(uint256).max - 1 mln` wrapper tokens.
5. Alice deposited 70k of the token amount to the wrapper.
6. The attacker burns 70k of wrapper tokens and steals Alice's tokens 

This attack vector can be also used for DoS.
1. The attacker creates a wrapper for the underlying token
2. The attacker has 1 wei in balance, and does first deposit with uint256 max amount.
3. Any deposit will revert due to overflow in total supply which is equal to uint256 max.

## Impact

Users funds can be stolen from `ChilizWrappedERC20`

## Code Snippet

```solidity
function depositFor(address account, uint256 amount) public virtual returns (bool) {
        if (address(underlyingToken) == address(0)) revert NotInitialized();
        address sender = _msgSender();
        if (sender == address(this)) revert CannotDeposit();
        SafeERC20.safeTransferFrom(underlyingToken, sender, address(this), amount);
        _mint(account, amount * decimalsOffset); // @audit amount might be less than the user tranfered 

        emit Deposit(account, amount);

        return true;
    }
```

## Tool used

Manual Review

## Recommendation
1. Use `balanceOf` before, and after to determine how many tokens should be minted. This will always result in an actual number of tokens transferred 
```solidity
function depositFor(address account, uint256 amount) public virtual returns (bool) {
        if (address(underlyingToken) == address(0)) revert NotInitialized();
        address sender = _msgSender();
        if (sender == address(this)) revert CannotDeposit();
        uint256 balanceBefore = underlyingToken.balanceOf(address(this));
        SafeERC20.safeTransferFrom(underlyingToken, sender, address(this), amount);
        uint256 balanceAfter = underlyingToken.balanceOf(address(this));
        uint256 amountTransferred = balanceAfter -  balanceBefore; 
        _mint(account, amountTransferred * decimalsOffset); 

        emit Deposit(account, amountTransferred);

        return true;
    }
```

2. Alternatively, add a require check that the amount is not uint max 256
