Fluffy Orchid Rooster

high

# Direct use of user input `amount` in `depositFor()` could result in the `ChilizWrappedERC20` instance becoming insolvent

## Summary

Wrapper core function `depositFor()` accepts amount as an input, and mints `amount` if safeTransferFrom() call succeeds, which can be a problem in several cases. Main case is tokens that could actually send amount less than was asked by user, and in this case wrapper becomes insolvent. 

`function depositFor(address account, uint256 amount) public virtual returns (bool)`

https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/utils/ChilizWrappedERC20.sol#L36-L38

## Vulnerability Detail

### Case 1: Stables with fees

Alice, Bob and Marley deposits USD with fee on transfer (imagine USDT with fee switch toggled).

Alice deposits 10,000 USDt to the contract, wrapper receives 9,981 USDt, Alice receives 10,000 wUSDt   // Total issued 10,000 wUSDT, wrapper balance  9,981

Bob deposits 10,000 USDt to the contract, wrapper receives 9,981 USDt, Bob receives 10,000 wUSDt       // Total issued 20,000 wUSDT, wrapper balance  19,962

Marley deposits 10,000 USDt to the contract, wrapper receives 9,981 USDt, Marley receives 10,000 wUSDt // Total issued 30,000 wUSDT, wrapper balance  29,943

Alice burns 10,000 wUSDt and withdraws 10,000 USDt form contract // contract balance 19,943

Bib burns 10,000 wUSDt and withdraws 10,000 USDt form contract // contract balance 9,943

Marley can’t reedem his 10,000 wUSDT 

<details>
 <summary>Foundry POC</summary>
 
```javascript
    function test_USDTWrapperInsolvency() public {
        vm.startPrank(usdtOwner);

        usdt.transfer(user0, 10_000 * 1e6);
        assert(usdt.balanceOf(user0) == 10_000 * 1e6);

        usdt.transfer(user1, 10_000 * 1e6);
        assert(usdt.balanceOf(user1) == 10_000 * 1e6);

        usdt.transfer(user3, 10_000 * 1e6);
        assert(usdt.balanceOf(user1) == 10_000 * 1e6);

        usdt.transfer(user4, 10_000 * 1e6);
        assert(usdt.balanceOf(user1) == 10_000 * 1e6);

        usdt.transfer(user5, 10_000 * 1e6);
        assert(usdt.balanceOf(user1) == 10_000 * 1e6);

        // setting  fee of 0.049
        usdt.setParams(19, 49);

        vm.startPrank(user0);
        wusdt = wrapperFactory.createWrappedToken(address(usdt));

        _deposit(ChilizWrappedERC20(wusdt), user3, 10_000 * 1e6); //
        console2.log("Balance of wUSDT of user3", ChilizWrappedERC20(wusdt).balanceOf(user3));
        console2.log("Wrapper USDT balance", usdt.balanceOf(wusdt));

        _deposit(ChilizWrappedERC20(wusdt), user4, 10_000 * 1e6);

        console2.log("Balance of wUSDT of user4", ChilizWrappedERC20(wusdt).balanceOf(user4));
        console2.log("Wrapper USDT balance", usdt.balanceOf(wusdt));

        _deposit(ChilizWrappedERC20(wusdt), user5, 10_000 * 1e6);

        console2.log("Balance of wUSDT of user5", ChilizWrappedERC20(wusdt).balanceOf(user5));
        console2.log("Wrapper USDT balance", usdt.balanceOf(wusdt));

        vm.startPrank(user3);
        ChilizWrappedERC20(wusdt).withdrawTo(user3, 10_000 * 1e18); //18 decimals wrapped chili token

        console2.log("Balance of USDT of user3", usdt.balanceOf(user3));
        console2.log("Wrapper USDT balance", usdt.balanceOf(wusdt));

        vm.startPrank(user4);
        ChilizWrappedERC20(wusdt).withdrawTo(user4, 10_000 * 1e18);

        console2.log("Balance of USDT of user4", usdt.balanceOf(user4));
        console2.log("Wrapper USDT balance", usdt.balanceOf(wusdt));

        vm.startPrank(user5);

        vm.expectRevert();
        ChilizWrappedERC20(wusdt).withdrawTo(user5, 10_000 * 1e18);

        console2.log("Balance of USDT of user5", usdt.balanceOf(user5));
        console2.log("Wrapper USDT balance", usdt.balanceOf(wusdt));
    }
    
    function _deposit(ChilizWrappedERC20 token, address user, uint256 amount) internal {
        vm.startPrank(user);
        _approveUSDT();
        token.depositFor(user, amount);
    }
```
  
```logs
Logs:
  Balance of wUSDT of user3 10000000000000000000000
  Wrapper USDT balance 9981000000
  Balance of wUSDT of user4 10000000000000000000000
  Wrapper USDT balance 19962000000
  Balance of wUSDT of user5 10000000000000000000000
  Wrapper USDT balance 29943000000
  Balance of USDT of user3 9981000000 <- less than 10k again because of fee
  Wrapper USDT balance 19943000000
  Balance of USDT of user4 9981000000
  Wrapper USDT balance 9943000000 
  Balance of USDT of user5 0 <- zero USDt received bacause withdraw was reverted
  Wrapper USDT balance 9943000000

```  
</details>

Insolvencty situation becomes worse with bigger amount of deposits

### Case 2: Transfer balanceOf(user) on very big number

Some tokens will transfer actual balance of tokens from user if user inputs type(uint256).max or other value bigger than balance. The case of type(uint256).max is not valid because it will throw on decimal offset calculation `_mint(account, amount * decimalsOffset);`, but any other case will do. 

## Impact

If user can in fact transfer less tokens that will be minted to his name, it leads to immediate 1:1 backup violation.

## Code Snippet

```javascript
  function depositFor(address account, uint256 amount) public virtual returns (bool) {
        if (address(underlyingToken) == address(0)) revert NotInitialized();
        address sender = _msgSender();
        if (sender == address(this)) revert CannotDeposit();
        SafeERC20.safeTransferFrom(underlyingToken, sender, address(this), amount); <- if safeTransferFrom doesn’t revert 
        _mint(account, amount * decimalsOffset); <- next line will mint a value from the user input

        emit Deposit(account, amount);

        return true;
    }
```
## Tool used

Manual Review, Foundry

## Recommendation

Recommend two ways of a problem mitigation 
- to add a check so received amount of tokens is always equal to input value from user, or
- to mint only amount of received tokens instead of amount from user input

 
