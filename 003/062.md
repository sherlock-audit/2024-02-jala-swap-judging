Obedient Violet Tadpole

high

# Preventing Intentional Overflow in `_update(...)` Could Lead to DOS of All Pool Functionality

## Summary
https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaPair.sol#L93-L107

## Vulnerability Detail
The `_update()` function updates the reserves and price accumulators. The code is an exact copy from Uniswap V2. However, Uniswap V2 uses Solidity 0.6, while Jala uses Solidity 0.8. Solidity 0.8 has a default overflow check for addition and multiplication, which may cause revert in `_update()` and DOS on all functionalities of JalaPair.

## Impact
The `_update()` function includes calculations that intentionally overflow. However, as Jala uses Solidity 0.8, it will revert these calculations. This action will cause the `_update()` function to always revert when updating price accumulators. Also, since the `_update()` function is called on all core functionalities like minting, burning, and swapping, it may lead to a DOS on all core functionalities of JalaPair.

## Code Snippet
```solidity
// update reserves and, on the first call per block, price accumulators
function _update(uint256 balance0, uint256 balance1, uint112 _reserve0, uint112 _reserve1) private {
    if (balance0 > type(uint112).max || balance1 > type(uint112).max) revert Overflow();
    uint32 blockTimestamp = uint32(block.timestamp % 2 ** 32);
    uint32 timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired
    if (timeElapsed > 0 && _reserve0 != 0 && _reserve1 != 0) {
        // * never overflows, and + overflow is desired
        price0CumulativeLast += uint256(UQ112x112.encode(_reserve1).uqdiv(_reserve0)) * timeElapsed;
        price1CumulativeLast += uint256(UQ112x112.encode(_reserve0).uqdiv(_reserve1)) * timeElapsed;
    }
    reserve0 = uint112(balance0);
    reserve1 = uint112(balance1);
    blockTimestampLast = blockTimestamp;
    emit Sync(reserve0, reserve1);
}
```

## Tool used
Manual Review

## Recommendation
Add 'unchecked' to prevent transaction reversion when calculation overflows.
```solidity
unchecked {
uint32 timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired
    if (timeElapsed > 0 && _reserve0 != 0 && _reserve1 != 0) {
        // * never overflows, and + overflow is desired
        price0CumulativeLast += uint256(UQ112x112.encode(_reserve1).uqdiv(_reserve0)) * timeElapsed;
        price1CumulativeLast += uint256(UQ112x112.encode(_reserve0).uqdiv(_reserve1)) * timeElapsed;
    }
}
```
