Atomic Lavender Flamingo

medium

# `removeLiquidityETHSupportingFeeOnTransferTokens` does not properly enforce `amountTokenMin`

## Summary
`removeLiquidityETHSupportingFeeOnTransferTokens` does not properly enforce `amountTokenMin` 

## Vulnerability Detail
`removeLiquidityETHSupportingFeeOnTransferTokens` is supposed to allow the user to remove liquidity and give them slippage protection, even when operating with Fee-On-Transfer tokens. 
The problem is that the `amountTokenMin` is checked against the tokens sent by the pair, rather the amount of tokens received by the user. In certain scenarios, due the fee-on-transfer, the user will receive less than what they had specified as `amountTokenMin`. 
```solidity
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    ) public virtual override ensure(deadline) returns (uint256 amountETH) {
        (, amountETH) = removeLiquidity(token, WETH, liquidity, amountTokenMin, amountETHMin, address(this), deadline);
        TransferHelper.safeTransfer(token, to, IERC20(token).balanceOf(address(this)));
        IWETH(WETH).withdraw(amountETH);
        TransferHelper.safeTransferETH(to, amountETH);
    }
```
```solidity
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    ) public virtual override ensure(deadline) returns (uint256 amountA, uint256 amountB) {
        address pair = JalaLibrary.pairFor(factory, tokenA, tokenB);
        IJalaPair(pair).transferFrom(msg.sender, pair, liquidity); // send liquidity to pair
        (uint256 amount0, uint256 amount1) = IJalaPair(pair).burn(to);
        (address token0, ) = JalaLibrary.sortTokens(tokenA, tokenB);
        (amountA, amountB) = tokenA == token0 ? (amount0, amount1) : (amount1, amount0);
        if (amountA < amountAMin) revert InsufficientAAmount();
        if (amountB < amountBMin) revert InsufficientBAmount();
    }
```

Furthermore, the `amountTokenMin` check is made against the values the `pair` has sent, but not the ones which the router has received. Since there are 2 transfers (pair -> router -> user), the router does not account for not just one, but two transfer taxes.



## Impact
`removeLiquidityETHSupportingFeeOnTransferTokens`'s `amountTokenMin` will not work as expected 

## Code Snippet
https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/JalaRouter02.sol#L196

## Tool used

Manual Review

## Recommendation
check the `amountTokenMin` against the user's final change in balance 
