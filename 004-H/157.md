Decent Marmalade Rattlesnake

high

# `JalaMasterRouter::swapExactTokensForETH` swaps token with not correct decimals, which cause to losing tokens by trader

## Summary
`JalaMasterRouter::swapExactTokensForETH` function accept the underlying token amount to swap. It then should call router `swapExactTokensForETH`  function with the wrapped token of the underlying token, not underlying itself, thus the contract takes traders tokens and do not pay back.

## Vulnerability Detail
When the trader calls `JalaMasterRouter::swapExactTokensForETH` function, he/she passes as an argument the amount of underlying token. After that the function should convert the underlying token to respective wrapped token. It is wrapped by `ChilizWrapperFactory` contract. So finally, `JalaMasterRouter::swapExactTokensForETH` function should call router `swapExactTokensForETH` function with wrapped token amount, which differs with decimals amount from underlying token. 

`JalaMasterRouter::swapExactTokensForETH` function [does wrap underlying token and does approve](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L296C1-L297C55) correct amount of wrapped token, but mistakely sends `amountIn` value instead of `IERC20(wrappedTokenIn).balanceOf(address(this))`.

Because underlying token can have 1 decimals and wrapped token 18 decimals, the user can send x amount to contract, but actually swap x/17 amount, which is 17 times less from what user wants.

## Impact
User can lose 17x amount of tokens he/she sends to the contract for swap

## Code Snippet
https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L300
```solidity
 amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn, amountOutMin, path, to, deadline);
```

## Proof of Concept

The test shows that the swap function in called with `amountIn` with 10 decimals, but should be called with 18 decimals of the wrapped token. Consoled amount shows how many user loses

<details>
  <summary>Test</summary>

```solidity
    function test_swapExactTokensForETHMyTest() public {
        JalaFactory jFactory = new JalaFactory(PLAYER1);
        ChilizWrapperFactory cFactory = new ChilizWrapperFactory();

        MockWETH WETH = new MockWETH();
        vm.startPrank(PLAYER2);
        WETH.deposit{value: 4e18}();
        vm.stopPrank();

        ERC20Mintable ChilizOne = new ERC20Mintable("C1", "C1", 10);
        ERC20Mintable ChilizTwo = new ERC20Mintable("C2", "C2", 10);
        ChilizOne.mint(4e10, address(PLAYER2));
        ChilizTwo.mint(4e10, address(PLAYER2));

        JalaRouter02 router = new JalaRouter02(address(jFactory), address(WETH));

        JalaMasterRouter masterRouter = new JalaMasterRouter(
            address(jFactory),
            address(cFactory),
            address(router),
            address(WETH)
        );

        vm.startPrank(PLAYER2);
        ChilizOne.approve(address(masterRouter), 2e10);
        masterRouter.wrapTokenAndaddLiquidityETH{value: 2e18}(address(ChilizOne), 2e10, 0, 0, PLAYER2, block.timestamp);

        ChilizOne.approve(address(masterRouter), 1e10);
        address wrappedChilizOne = ChilizWrapperFactory(cFactory).wrappedTokenFor(address(ChilizOne));
        address[] memory path = new address[](2);
        path[0] = wrappedChilizOne;
        path[1] = address(WETH);

        // actually received amounts, user swaps 1e10 which is half of the pair reserve, so he/she expect out ~half of WETH
        uint256[] memory amounts = masterRouter.swapExactTokensForETH(address(ChilizOne), 1e10, 0, path, PLAYER2, block.timestamp);
        //10000000000 - amount user send for swap, it should have 18 decimals, as wrapped token has, but here only 10 decimals
        //9969999950 - user receives much less tha a half of the WETH reserves
        console.logUint(amounts[0]);
        console.logUint(amounts[1]);

        uint decimalsOffset = ChilizWrappedERC20(wrappedChilizOne).getDecimalsOffset();
        
        // What should be
        uint256[] memory amounts2 = router.getAmountsOut(1e10*decimalsOffset, path);
        //1000000000000000000 - here what should be sent in the swap- 18 decimals
        //665331993128658698 - user will receive enough of WETH
        console.logUint(amounts2[0]);
        console.logUint(amounts2[1]);

        assertEq(amounts[0], amounts2[0]);
        assertEq(amounts[1], amounts2[1]);
        
        vm.stopPrank();
    }
```

</details>

## Tool used

Manual Review

## Recommendation
Replace `amountIn` to `IERC20(wrappedTokenIn).balanceOf(address(this))`

```diff
- amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn, amountOutMin, path, to, deadline);
+ amounts = IJalaRouter02(router).swapExactTokensForETH(IERC20(wrappedTokenIn).balanceOf(address(this)), amountOutMin, path, to, deadline);
```