Trendy Sandstone Griffin

high

# Token theft from `JalaMasterRouter` due to incorrect scaling during calls to `swapExactTokensForETH`.

## Summary

Calls to [`swapExactTokensForETH`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L284) result in orphaned [`ChilizWrappedERC20`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/utils/ChilizWrappedERC20.sol) tokens left on the [`JalaMasterRouter`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol), which can subsequently be stolen by an attacker.

## Vulnerability Detail

In the snippet below, we show the full contents of the [`swapExactTokensForETH`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L284) function:

### ðŸ“„ [JalaMasterRouter.sol](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol)

```solidity
function swapExactTokensForETH(
    address originTokenAddress,
    uint256 amountIn,
    uint256 amountOutMin,
    address[] calldata path,
    address to,
    uint256 deadline
) external virtual override returns (uint256[] memory amounts) {
    address wrappedTokenIn = IChilizWrapperFactory(wrapperFactory).wrappedTokenFor(originTokenAddress);

    require(path[0] == wrappedTokenIn, "MS: !path");

    TransferHelper.safeTransferFrom(originTokenAddress, msg.sender, address(this), amountIn);
    _approveAndWrap(originTokenAddress, amountIn);
    IERC20(wrappedTokenIn).approve(router, IERC20(wrappedTokenIn).balanceOf(address(this)));

    amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn, amountOutMin, path, to, deadline);
}
```

Here, we must emphasise that the `originTokenAddress` passed into the function is **not** the address of a [`ChilizWrappedERC20`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/utils/ChilizWrappedERC20.sol), as we can see `wrappedTokenIn` gets computed based on the provided token address (from which the input tokens are subsequently wrapped).

Importantly, the following line **does** transact using wrapped tokens (`path[0]` is validated to point to the `wrappedTokenIn`):

```solidity
amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn, amountOutMin, path, to, deadline)
```

The effects of this relationship are as follows:

1. Let's imagine the user sends $100 \text{ USDC}$ to the contract. USDC having $6$ decimal places will correspond to a [`ChilizWrappedERC20`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/utils/ChilizWrappedERC20.sol) amount of `100 ether`.
2. A call to ` _approveAndWrap(originTokenAddress, amountIn)` ensures a wrapped token balance of `100 ether` now rests on the [`JalaMasterRouter`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol).
3. Now, when the call to `swapExactTokensForETH`, the [`JalaMasterRouter`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol) specifes the original unwrapped `amountIn` for the swap, meaning that instead of swapping `100e18` of [`ChilizWrappedERC20`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/main/jalaswap-dex-contract/contracts/utils/ChilizWrappedERC20.sol), it only swaps `100e6` of them, leaving the remaining `99999999999900000000` of wrapped tokens on the  [`JalaMasterRouter`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol)(!)

In this case, the user has unknowingly left 99% of their wrapped token value like a sitting duck on the [`JalaMasterRouter`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol).

An attacker can subsequently exploit this vulnerability by executing any of the functions which indirectly makes a call to [`_unwrapAndTransfer`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L303), as this will collect any remaining balance on the contract and forward this to the caller.

By transacting in units of the stuck token (i.e. by providing a negligible amount of liquidity), an attacker can steal this orphaned value:

```solidity
function _unwrapAndTransfer(
    address wrappedTokenOut,
    address to
) private returns (address reminderTokenAddress, uint256 reminder) {
    uint256 balanceOut = IERC20(wrappedTokenOut).balanceOf(address(this));
    if (balanceOut == 0) return (reminderTokenAddress, reminder);

    uint256 tokenOutOffset = IChilizWrappedERC20(wrappedTokenOut).getDecimalsOffset();
    uint256 tokenOutReturnAmount = (balanceOut / tokenOutOffset) * tokenOutOffset;

    IERC20(wrappedTokenOut).approve(wrapperFactory, tokenOutReturnAmount); // no need for check return value, bc addliquidity will revert if approve was declined.

    if (tokenOutReturnAmount > 0) {
        IChilizWrapperFactory(wrapperFactory).unwrap(to, wrappedTokenOut, tokenOutReturnAmount);
    }

@>  // transfer dust as wrapped token
@>  if (IERC20(wrappedTokenOut).balanceOf(address(this)) > 0) {
@>      reminderTokenAddress = address(wrappedTokenOut);
@>      reminder = IERC20(wrappedTokenOut).balanceOf(address(this));
@>      TransferHelper.safeTransfer(wrappedTokenOut, to, IERC20(wrappedTokenOut).balanceOf(address(this)));
@>  }

}
```

## Impact

High.

## Code Snippet

```solidity
function swapExactTokensForETH(
    address originTokenAddress,
    uint256 amountIn,
    uint256 amountOutMin,
    address[] calldata path,
    address to,
    uint256 deadline
) external virtual override returns (uint256[] memory amounts) {
    address wrappedTokenIn = IChilizWrapperFactory(wrapperFactory).wrappedTokenFor(originTokenAddress);

    require(path[0] == wrappedTokenIn, "MS: !path");

    TransferHelper.safeTransferFrom(originTokenAddress, msg.sender, address(this), amountIn);
    _approveAndWrap(originTokenAddress, amountIn);
    IERC20(wrappedTokenIn).approve(router, IERC20(wrappedTokenIn).balanceOf(address(this)));

    amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn, amountOutMin, path, to, deadline);
}
```

## Tool used

Foundry

## Recommendation

Ensure the internal call to [`swapExactTokensForETH`](https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaMasterRouter.sol#L284) correctly handles scaled amounts:

```diff
+  uint256 tokenOffset = IChilizWrappedERC20(wrappedTokenIn).getDecimalsOffset();
+
- amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn, amountOutMin, path, to, deadline);
+ amounts = IJalaRouter02(router).swapExactTokensForETH(amountIn * tokenOffset, amountOutMin, path, to, deadline);
```

