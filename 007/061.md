Rough Jade Shrimp

medium

# JalaPair.sol#mint() - Migrators can pull off inflation attacks on the protocol

## Summary
Migrators can pull off inflation attacks on the protocol

## Vulnerability Detail
This issue is almost identical to [this one](https://code4rena.com/reports/2022-06-canto#m-08-zeroswapuniswapv2pairsol-token-reserves-per-lp-token-can-be-manipulated-due-to-lack-of-minimum_liquidity-when-minting-the-first-liquidity-with-migrator).

Since the code is a fork of UniV2, let's take a look at `mint`  there.
```solidity
function mint(address to) external lock returns (uint liquidity) {
        (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
        uint balance0 = IERC20(token0).balanceOf(address(this));
        uint balance1 = IERC20(token1).balanceOf(address(this));
        uint amount0 = balance0.sub(_reserve0);
        uint amount1 = balance1.sub(_reserve1);

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
->    if (_totalSupply == 0) {
            liquidity = Math.sqrt(amount0.mul(amount1)).sub(MINIMUM_LIQUIDITY);
           _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = Math.min(amount0.mul(_totalSupply) / _reserve0, amount1.mul(_totalSupply) / _reserve1);
        }
        require(liquidity > 0, 'UniswapV2: INSUFFICIENT_LIQUIDITY_MINTED');
        _mint(to, liquidity);

        _update(balance0, balance1, _reserve0, _reserve1);
        if (feeOn) kLast = uint(reserve0).mul(reserve1); // reserve0 and reserve1 are up-to-date
        emit Mint(msg.sender, amount0, amount1);
    }
```

You'll notice that there is a special case when `_totalSupply == 0`, it will then mint a MINIMUM_LIQUIDITY amount of tokens to the 0 address, this is done to lock up some initial tokens so that an inflation attack can't occur.

Looking at `mint` inside JalaSwap.
```solidity
 function mint(address to) external lock returns (uint256 liquidity) {
        (uint112 _reserve0, uint112 _reserve1, ) = getReserves(); // gas savings
        uint256 balance0 = IERC20(token0).balanceOf(address(this));
        uint256 balance1 = IERC20(token1).balanceOf(address(this));
        uint256 amount0 = balance0 - _reserve0;
        uint256 amount1 = balance1 - _reserve1;

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint256 _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
->        if (IJalaFactory(factory).migrators(msg.sender)) {
                liquidity = IMigrator(msg.sender).desiredLiquidity();
                if (liquidity == 0 || liquidity == type(uint).max) revert BadDesiredLiquidity();
            } else {
                liquidity = Math.sqrt(amount0 * amount1) - MINIMUM_LIQUIDITY;
                _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
            }
        } else {
            liquidity = Math.min((amount0 * _totalSupply) / _reserve0, (amount1 * _totalSupply) / _reserve1);
        }
        if (liquidity == 0) revert InsufficientLiquidityMinted();
        _mint(to, liquidity);

        _update(balance0, balance1, _reserve0, _reserve1);
        if (feeOn) kLast = uint256(reserve0) * reserve1; // reserve0 and reserve1 are up-to-date
        emit Mint(msg.sender, amount0, amount1);
    }
```

You can see that there is extra logic inside the if statement for `totalSupply == 0`. Basically it checks if msg.sender is a migrator and retrieves a `desiredLiqudity` amount which will then be minted to the `to` address. Notice that the initial minting of tokens to address 0 are skipped if this occurs.

This means that a malicious migrator can pull of an inflation attack on any `JalaPair`.

* Mint `1 wei` of lp tokens
* Add a large amount (`1e20` for example) of `token0` and `token1`, by sending the tokens directly to the contract
* Call `sync` to update the reserves.

At this point `1 wei` of lp tokens is worth `1e20` of `token0` and `token1`. If a user deposits less than `1e20` of both tokens, his tx will revert, if he sends more than `1e20` he will lose a massive portion of his tokens.

Example:

` liquidity = Math.min((amount0 * _totalSupply) / _reserve0, (amount1 * _totalSupply) / _reserve1);`

* If the user sends less then `1e20`.
`liquidity = Math.min(1e19 * 1)/ 1e20), (1e19 * 1) / 1e20)`, as you can see the left side is smaller than the right for both tokens, so `liquidity = 0`, which revert on the next line.

* If the user sends a bit more then `1e20`
`liquidity = Math.min(1.99e20 * 1)/ 1e20), (1.99e20 * 1) / 1e20)`, there will be a remainder of 0.99e20 tokens that will be lost.
`liquidity = 1`

Note that the README of the contest doesn't state anything about migrators being TRUSTED.

## Impact
DoS of the minting logic for a `JalaPair`.
Loss of funds for users

## Code Snippet
https://github.com/sherlock-audit/2024-02-jala-swap/blob/030d3ed54214754301154bce0e58ea534100a7e3/jalaswap-dex-contract/contracts/JalaPair.sol#L142-L144

## Tool used
Manual Review

## Recommendation
Either remove the migrator logic or modify it in a way that when `totalSupply == 0` it will always mint the `MINIMUM_LIQUDITIY` amount of lp tokens.
